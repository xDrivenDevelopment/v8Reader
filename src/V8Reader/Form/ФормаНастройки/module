Перем Версия;

Процедура ЗаписатьНастройкиОбработки()
	Если  ПрограммаDiff<>"" и НастройкаСтрокиЗапуска<>"" тогда
		ВыборDiff=Новый Структура;
		ВыборDiff.Вставить("ПутьПрограммы",ПрограммаDiff);
		ВыборDiff.Вставить("ИспользоватьМетки",ИспользоватьМетки);
		ВыборDiff.Вставить("СтрокаЗапуска",НастройкаСтрокиЗапуска);
	Иначе
		ВыборDiff=Неопределено;
	КонецЕсли;
	СохранитьЗначение("V8Reader|ВыборDiff",ВыборDiff);
	СохранитьЗначение("V8Reader|ПроверятьАктуальнуюВерсиюПриОткрытии",ПроверятьАктуальнуюВерсиюПриОткрытии);
	ЭтаФорма.ВладелецФормы.ИзменитьЗаголовокDiff();
	СохранитьЗначение("V8Reader|ИспользоватьКэш",ИспользоватьКэш);
	СохранитьЗначение("V8Reader|СтруктурноеСравнениеМодулей",СтруктурноеСравнениеМодулей);
	
КонецПроцедуры

Процедура ОсновныеДействияФормыЗаписатьИЗакрыть(Кнопка)
	ЗаписатьНастройкиОбработки();
	Закрыть();
КонецПроцедуры

Процедура ОсновныеДействияФормыЗаписать(Кнопка)
	ЗаписатьНастройкиОбработки();
КонецПроцедуры

Процедура ВыборDiffНачалоВыбора(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	ДиалогВыбора = Новый ДиалогВыбораФайла(РежимДиалогаВыбораФайла.Открытие);
	ДиалогВыбора.Фильтр = "Файлы приложений (*.exe)|*.exe"; 
	ДиалогВыбора.ПолноеИмяФайла=Элемент.Значение;
	ДиалогВыбора.Заголовок="Выберите приложение для трехуровневого сравнения файлов";
	Если ДиалогВыбора.Выбрать() Тогда
		ФайлDiff=Новый файл(ДиалогВыбора.ПолноеИмяФайла);
		Элемент.Значение = ДиалогВыбора.ПолноеИмяФайла;
		Если ФайлDiff.Имя="kdiff3.exe" или ФайлDiff.Имя="sgdm.exe" тогда
			ЭлементыФормы.ИспользоватьМетки.Видимость=истина;
			ИспользоватьМетки=Истина;
		Иначе
			ЭлементыФормы.ИспользоватьМетки.Видимость=ложь;
			ИспользоватьМетки=ложь;
		КонецЕсли;
		ЗапуститьКонструкторСтрокиЗапуска(ФайлDiff);
	КонецЕсли;
	
КонецПроцедуры

Процедура НастройкаСтрокиЗапускаНачалоВыбора(Элемент, СтандартнаяОбработка)
	ВвестиСтроку(НастройкаСтрокиЗапуска,"Строка запуска программы для 3-way diff",,истина);
КонецПроцедуры

Процедура ЗапуститьКонструкторСтрокиЗапуска(ФайлDiff)
	Если ФайлDiff.Имя="kdiff3.exe" тогда
		Если ИспользоватьМетки=Истина тогда
			НастройкаСтрокиЗапуска=""""+ФайлDiff.ПолноеИмя+""" ""%1"" -fname ""синоним%1 (первый файл)"" ""%2"" -fname ""синоним%2 (второй файл)"" ""%3"" -fname ""синоним%3 (третий файл)""";
		Иначе
			НастройкаСтрокиЗапуска=""""+ФайлDiff.ПолноеИмя+""" ""%1"" ""%2"" ""%3""";
		КонецЕсли;
		
	ИначеЕсли  ФайлDiff.Имя="sgdm.exe" тогда
		Если ИспользоватьМетки=Истина тогда
			НастройкаСтрокиЗапуска = """"+ФайлDiff.ПолноеИмя+""" /title1=""синоним%1 (первый файл)"" /title2=""синоним%2 (второй файл)"" /title3=""синоним%3 (третий файл)"" ""%1"" ""%2"" ""%3""";
		Иначе
			НастройкаСтрокиЗапуска=""""+ФайлDiff.ПолноеИмя+""" ""%1"" ""%2"" ""%3""";
		КонецЕсли;
	Иначе
		НастройкаСтрокиЗапуска=""""+ФайлDiff.ПолноеИмя+""" ""%1"" ""%2"" ""%3""";
	КонецЕсли;
КонецПроцедуры

Процедура ПриОткрытии()
	Если ВыборDiff<>Неопределено тогда
		Попытка
			ЭлементыФормы.ПрограммаDiff.Значение=ВыборDiff.ПутьПрограммы;
			ФайлDiff=Новый файл(ПрограммаDiff);
			Если ФайлDiff.Имя="kdiff3.exe" или ФайлDiff.Имя="sgdm.exe" тогда
				ЭлементыФормы.ИспользоватьМетки.Видимость=истина;
			КонецЕсли;
			ЭлементыФормы.ИспользоватьМетки.Значение=ВыборDiff.ИспользоватьМетки;
			ЭлементыФормы.НастройкаСтрокиЗапуска.Значение=ВыборDiff.СтрокаЗапуска;
		Исключение
		КонецПопытки;
	КонецЕсли;
КонецПроцедуры

Процедура ИспользоватьМеткиПриИзменении(Элемент)
	ФайлDiff=Новый Файл(ПрограммаDiff);
	ЗапуститьКонструкторСтрокиЗапуска(ФайлDiff);
КонецПроцедуры

Процедура ПроверитьСейчасНажатие(Элемент) Экспорт 
	
	Попытка
		ПутьКВремФайлу = ПолучитьИмяВременногоФайла();
		Шел=Новый COMОбъект("WScript.Shell");
		СтрокаЗапуска = "cmd /c ping -n 1 ya.ru > "+ПутьКВремФайлу;
		Шел.run(СтрокаЗапуска,0,1);
		ТекстФайла = Новый ТекстовыйДокумент;
		ТекстФайла.Прочитать(ПутьКВремФайлу);
		strPingResults = НРег(ТекстФайла.ПолучитьТекст());
		Если  Найти(strPingResults, "ttl=")>0 Тогда
			WinHttp = Новый COMОбъект("WinHttp.WinHttpRequest.5.1");
			WinHttp.SetClientCertificate("localhost");
			WinHttp.Open("GET","https://api.github.com/repos/xDrivenDevelopment/v8reader/commits?per_page=1",0);
			WinHttp.Send();
			ТД=Новый ТекстовыйДокумент;
			ТД.УстановитьТекст(WinHttp.ResponseText);
			РегулярноеВыражениеX=Новый COMОбъект("VBScript.RegExp");
			РегулярноеВыражениеX.Global = Истина;
			РегулярноеВыражениеX.IgnoreCase = Истина;
			РегулярноеВыражениеX.MultiLine = Истина;
			РегулярноеВыражениеX.Pattern="""date"":""([^""]+)""";
			Наборы=РегулярноеВыражениеX.Execute(ТД.ПолучитьТекст());
			ВерсияНовая=Наборы.Item(0).SubMatches(0);
			
			Если Дата(Лев(ВерсияНовая,4),Сред(ВерсияНовая,6,2),Сред(ВерсияНовая,9,2))>Дата(Версия) Тогда
				ПоказатьОповещениеПользователя(,, " Обнаружена новая версия
				| от " + Лев(ВерсияНовая,10) + "
				| Ссылка на страницу разработки
				| находится в форме настройки.",
				ЭлементыФормы.ПолеКартинки1.Картинка);
			ИначеЕсли Элемент <> Неопределено Тогда
				ПоказатьОповещениеПользователя(,, " Обработка V8Reader 
				| последней версии.
				| Обновление не требуется.",ЭлементыФормы.ПолеКартинки.Картинка);
			КонецЕсли;
		ИначеЕсли Элемент <> Неопределено Тогда
			ПоказатьОповещениеПользователя(,, " Отсутствует 
			| подключение к интернету.",ЭлементыФормы.ПолеКартинки1.Картинка);
		КонецЕсли;
	Исключение
		Если Элемент <> Неопределено Тогда
			ПоказатьОповещениеПользователя(,, " В Вашем программном окружении
			| не поддерживается
			| объект WinHttpRequest.",ЭлементыФормы.ПолеКартинки1.Картинка);
		КонецЕсли;

	КонецПопытки;
	
	
КонецПроцедуры

Процедура ПерейтиНаСтраницуРазработкиНажатие(Элемент)
	ЗапуститьПриложение("https://github.com/xDrivenDevelopment/v8Reader");
	//ЗапуститьПриложение("http://infostart.ru/public/106310/");
КонецПроцедуры

СтраницаРазработки="http://infostart.ru/public/106310/";
Версия="10.06.2015 23:59:59";
ЭлементыФормы.АктуальнаяВерсия.Заголовок="V8Reader версия от "+Лев(Версия,10)+""